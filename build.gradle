import groovy.json.JsonSlurper

task checkLatestWrapper {
  doLast {
    final latestRelease = new JsonSlurper().parseText(new URL('https://services.gradle.org/versions/current').text)
    println "Latest version is ${latestRelease.version}"
    println "Current version is ${gradle.gradleVersion}"
    wrapper.gradleVersion = latestRelease.version

    final currentJarSHA = file('gradle/wrapper/gradle-wrapper.jar').bytes.digest('SHA-256')
    final latestJarSHA = new URL(latestRelease.wrapperChecksumUrl).text
    final verifiedJarSHA = (currentJarSHA == latestJarSHA)

    file('README.md').text = """\
# Latest Gradle Wrapper 

[![Build Status](https://travis-ci.org/int128/latest-gradle-wrapper.svg?branch=master)](https://travis-ci.org/int128/latest-gradle-wrapper)
[![Gradle Status](https://gradleupdate.appspot.com/int128/latest-gradle-wrapper/status.svg?branch=master)](https://gradleupdate.appspot.com/int128/latest-gradle-wrapper/status)

This has the latest version of [Gradle Wrapper](https://docs.gradle.org/current/userguide/gradle_wrapper.html).
It is continuously updated by Travis CI.

## Gradle ${gradle.gradleVersion}

${verifiedJarSHA ? '✅' : '❌'} Checksum of `gradle/wrapper/gradle-wrapper.jar`:

- Current = `${currentJarSHA}`
- [Latest](${latestRelease.wrapperChecksumUrl}) = `${latestJarSHA}`
"""
  }
}

wrapper.dependsOn checkLatestWrapper
